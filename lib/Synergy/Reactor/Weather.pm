use v5.34.0;
package Synergy::Reactor::Weather;

use utf8;

use Moose;
with 'Synergy::Role::Reactor::EasyListening';

use experimental qw(signatures);
use namespace::clean;

use Synergy::Logger '$Logger';
use URI::Escape;
use JSON::MaybeXS;

has api_token => (
  is       => 'ro',
  isa      => 'Str',
  required => 1,
);

has locations => (
  is       => 'ro',
  isa      => 'ArrayRef',
  required => 1,
);

sub listener_specs {
  return {
    name      => 'weather',
    method    => 'handle_weather',
    exclusive => 1,
    targeted  => 1,
    predicate => sub ($self, $e) { $e->text =~ /\Aweather\b/i; },
    help_entries => [{
      title => 'weather',
      text  => "*weather*: what's the weather like in all the places?"
    }],
  };
}

# https://openweathermap.org/weather-conditions
my %icons = (
  '01d' => 'â˜€ï¸:', # clear sky
  '02d' => 'ðŸŒ¤', # few clouds
  '03d' => 'â˜ï¸:', # scattered clouds
  '04d' => 'ðŸŒ¥', # broken clouds
  '09d' => 'ðŸŒ¦', # shower rain
  '10d' => 'ðŸŒ§', # rain
  '11d' => 'â›ˆ:', # thunderstorm
  '13d' => 'ðŸŒ¨', # snow
  '50d' => 'ðŸŒ«', # mist

  '01n' => 'ðŸŒ•', # clear sky
  '02n' => '',   # few clouds
  '03n' => 'â˜ï¸',  # scattered clouds
  '04n' => '',   # broken clouds
  '09n' => '',   # shower rain
  '10n' => 'ðŸŒ§', # rain
  '11n' => 'â›ˆ:', # thunderstorm
  '13n' => 'ðŸŒ¨', # snow
  '50n' => 'ðŸŒ«', # mist
);

my @bearing = qw(
  N NNE NE ENE
  E ESE SE SSE
  S SSW SW WSW
  W WNW NW NNW
);

sub handle_weather ($self, $event) {
  $event->mark_handled;

  return $event->reply(join "\n",
    "Current weather:",
    map {
      $self->format_weather($_)
    } $self->locations->@*,
  );
}

sub format_weather ($self, $location) {
  my $res = $self->hub->http_get(
    "https://api.openweathermap.org/data/2.5/weather?q=".uri_escape($location)."&APPID=".$self->api_token
  )->get;

  unless ($res->is_success) {
    $Logger->log([ "error fetching weather for $location: %s", $res->as_string ]);
    return;
  }

  my $data = decode_json($res->content);

  # Melbourne ðŸ‡¦ðŸ‡º: ðŸŒ¡ 15â„ƒ/47â„‰ ðŸ’§ 67% ðŸ’¨ 26km/h WSW ðŸŒ§ Rain

  my $place = $data->{name};
  my $flag = country_to_flag($data->{sys}{country});
  my $temp_c = kelvin_to_celsius($data->{main}{temp});
  my $temp_f = kelvin_to_fahrenheit($data->{main}{temp});
  my $humidity = $data->{main}{humidity};
  my $wind_speed_kph = ms_to_kmh($data->{wind}{speed});
  my $wind_speed_mph = $wind_speed_kph * 0.62;
  my $wind_dir = $bearing[$data->{wind}{deg} / 25.5];
  my $icon = $icons{$data->{weather}[0]{icon}}; # XXX day/night according to UTC time, adjust
  my $desc = $data->{weather}[0]{main};

  return join("\N{EM SPACE}",
    sprintf("*%s %s:*", $flag, $place),
    sprintf("ðŸŒ¡ %dâ„ƒ/%dâ„‰", $temp_c, $temp_f),
    sprintf("ðŸ’§ %d%%", $humidity),
    sprintf("ðŸ’¨ %dkph/%dmph %s", $wind_speed_kph, $wind_speed_mph, $wind_dir),
    sprintf("%s %s", $icon, $desc),
  );
}

sub kelvin_to_celsius ($k) {
  return $k - 273;
}

sub kelvin_to_fahrenheit ($k) {
  return (9/5) * ($k - 273) + 32;
}

sub ms_to_kmh ($ms) {
  return $ms * 3.6;
}

sub country_to_flag ($cc) {
  return join('', map { pack 'U', 0x1f1e6+ord(lc($_))-0x61 } split(//, $cc) );
}

1;
