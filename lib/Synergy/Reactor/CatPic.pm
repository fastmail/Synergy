use v5.34.0;
use warnings;
package Synergy::Reactor::CatPic;

use utf8;

use Moose;
use DateTime;
with 'Synergy::Role::Reactor::CommandPost';

use Synergy::CommandPost;

use Synergy::Logger '$Logger';

use experimental qw(lexical_subs signatures);

my $EMOJI_CONFIG = <<'END_EMOJI';
ðŸ€ rat
ðŸ­ mouse
ðŸ mouse          mouse2
ðŸ‚ ox
ðŸƒ water_buffalo
ðŸƒ buffalo        water_buffalo
ðŸ„ cow            cow2
ðŸ® cow
ðŸ… tiger          tiger2
ðŸ¯ tiger
ðŸ† leopard
ðŸ‡ rabbit         rabbit2
ðŸ° rabbit
ðŸˆ cat            cat2
ðŸ± cat
ðŸ‰ dragon
ðŸ² dragon         dragon_face
ðŸŠ crocodile
ðŸ‹ whale          whale2
ðŸ³ whale
ðŸŒ snail
ðŸ snake
ðŸŽ horse          racehorse
ðŸ´ horse
ðŸ ram
ðŸ goat
ðŸ‘ sheep
ðŸ’ monkey
ðŸµ monkey         monkey_face
ðŸ™ˆ monkey         see_no_evil
ðŸ™‰ monkey         hear_no_evil
ðŸ™Š monkey         speak_no_evil
ðŸ“ rooster
ðŸ” chicken
ðŸ¥š chicken        egg
ðŸ¶ dog
ðŸ• dog            dog2
ðŸ– pig            pig2
ðŸ¥“ pig            bacon
ðŸ— boar
ðŸ˜ elephant
ðŸ™ octopus
ðŸ› bug
ðŸœ ant
ðŸ¦— cricket
ðŸ•· spider
ðŸ bee
ðŸž ladybug        beetle
ðŸŸ fish
ðŸ  fish           tropical_fish
ðŸ¡ fish           blowfish
ðŸ¡ blowfish
ðŸ¢ turtle
ðŸ£ chick          hatching_chick
ðŸ¤ chick          baby_chick
ðŸ¥ chick          hatched_cick
ðŸ¦ bird
ðŸ§ penguin
ðŸ¨ koala
ðŸ© poodle
ðŸ© dog            poodle
ðŸª camel          dromedary_camel
ðŸ« camel
ðŸ« perl           camel
ðŸ¬ dolphin
ðŸ· pig
ðŸ¸ frog
ðŸ¹ hamster
ðŸº wolf
ðŸ» bear
ðŸ¼ panda
ðŸ¿ chipmunk
ðŸ¦€ crab
ðŸ¦ lion
ðŸ¦‚ scorpion
ðŸ¦ƒ turkey
ðŸ¦„ unicorn
ðŸ¦… eagle
ðŸ¦† duck
ðŸ¦‡ bat
ðŸ¦ˆ shark
ðŸ¦‰ owl
ðŸ¦Š fox            fox_face
ðŸ¦‹ butterfly
ðŸ¦Œ deer
ðŸ¦ gorilla
ðŸ¦Ž lizard
ðŸ¦ rhinoceros
ðŸ¦ shrimp
ðŸ¦‘ squid
ðŸ¦“ zebra
ðŸ¦’ giraffe        giraffe_face
ðŸ¦” hedgehog
ðŸ¦• sauropod
ðŸ¦– trex           t-rex
ðŸ¦– t-rex          t-rex
ðŸ¦— cricket
ðŸ¦• dinosaur       sauropod
ðŸ¦– dinosaur       t-rex

ðŸ•Š dove

ðŸ¦  raccoon
ðŸ¦™  llama
ðŸ¦›  hippo          hippopotamus
ðŸ¦›  hippopotamus
ðŸ¦˜  kangaroo
ðŸ¦˜  roo            kangaroo
ðŸ¦¡  badger
ðŸ¦¢  swan
ðŸ¦š  peacock
ðŸ¦œ  parrot
ðŸ¦ž  lobster
ðŸ¦Ÿ  mosquito
ðŸ¦Ÿ  skeeter        mosquito
ðŸ§¸  teddy
ðŸ¦   microbe
END_EMOJI

has _reactions => (
  is    => 'ro',
  isa   => 'HashRef',
  lazy  => 1,
  traits  => [ 'Hash' ],
  builder => '_build_reactions',
  handles => {
    reaction_for     => 'get',
    has_reaction_for => 'exists',
  },
);

my sub register_pic_line ($registry, $line) {
  my ($name, %to_set);

  ($to_set{emoji}, $name, $to_set{slackname}) = split /\s+/, $line;
  $to_set{slackname} //= $name;

  my $e = $registry->{$name} ||= { emoji => [], slackname => [] };

  for my $type (qw( emoji slackname )) {
    push $e->{$type}->@*, $to_set{$type}
      unless grep {; $_ eq $to_set{$type} } $e->{$type}->@*;
  }

  return;
}

sub _built_in_reactions {
  state %reactions;
  return {%reactions} if %reactions;

  register_pic_line(\%reactions, $_) for split /\n+/, $EMOJI_CONFIG;

  return {%reactions};
}

has extra_reactions_file => (
  is  => 'ro',
  isa => 'Str',
  predicate => 'has_extra_reactions_file',
);

sub _build_reactions ($self, @) {
  my $reactions = $self->_built_in_reactions;

  if ($self->has_extra_reactions_file) {
    my $file = $self->extra_reactions_file;
    open my $fh, '<', $file or confess("can't open $file for reading: $!");
    my $contents = do { local $/; <$fh> };
    close $fh;
    register_pic_line($reactions, $_) for split /\n+/, $contents;
  }

  return $reactions;
}

# Copied cat_pic code
# Because there was no documentation
# Works on terminal, dunno if slack wil be able to display it as an image
# Tried to test it on local slack, but the documentation was pretty bad with the config files
responder llama_pic => {
  exclusive => 1, # No idea what this is.
  targeted  => 1, # Or this
  help_titles => [ 'llama pic' ],
  help      => '*llama pic*: get a picture of a llama',
  matcher   => sub ($text, @) {
    return unless $text =~ /\Allama\spic\z/i;
    return [];
  },
}, sub ($self, $event) {
  $event->mark_handled;

  my $http_future = $self->hub->http_client->GET(
    "https://llama-as-a-service.vercel.app/llama_url"
  );

  return $http_future->on_done(sub($res)
  {
    if ($res->code == 200)
    {
      $event->reply($res->content);
      return;
    }
    $event->reply("Error while retrieving llama pictures!")
  });
};

responder llama_fax => {
  exclusive => 1, # No idea what this is.
  targeted  => 1, # Or this
  help_titles => [ 'llama fax' ],
  help      => '*llama fax*: get a fact about a llama',
  matcher   => sub ($text, @) {
    return unless $text =~ /\Allama\s(facts?|fax)\z/i;
    return [];
  },
}, sub ($self, $event) {
  $event->mark_handled;

  my $http_future = $self->hub->http_client->GET(
    "https://llama-as-a-service.vercel.app/llama_fax"
  );

  return $http_future->on_done(sub($res)
  {
    if ($res->code == 200)
    {
      $event->reply($res->content);
      return;
    }

    $event->reply("Error while retrieving llama fax!")
  });
};

responder cat_pic => {
  exclusive => 1,
  targeted  => 1,
  help_titles => [ 'cat pic' ],
  help      => '*cat pic*: get a picture of a cat',
  matcher   => sub ($text, @) {
    # TODO: make this an error instead of a give-up?
    return unless $text =~ /\Acat(?:\s+(pic|jpg|gif|png))?\z/i;
    return [ $1 || 'jpg,gif,png' ];
  },
}, sub ($self, $event, $fmt) {
  $event->mark_handled;

  $fmt = q{jpg,gif,png} if $fmt eq 'pic';

  my $http_future = $self->hub->http_client->GET(
    "https://api.thecatapi.com/api/images/get?format=src&type=$fmt",
    max_redirects => 0,
  );

  return $http_future->on_done(sub ($res) {
    if ($res->code =~ /\A3..\z/) {
      my $loc = $res->header('Location');
      $event->reply($loc);
      return;
    }

    $event->reply("Something went wrong getting the kitties! \N{CRYING CAT FACE}");
  });
};

listener misc_pic => sub ($self, $event) {
  my $text = $event->text;
  while ($text =~ /(\w+)\s+pic/ig) {
    my $name = lc $1;
    $Logger->log("looking for $name pic");
    next unless my $e = $self->reaction_for($name);

    my $exact = $text =~ /\A \s* $name \s+ pic \s* \z/x;

    # If this is all they said, okay.
    $event->mark_handled if $exact;

    my $emoji  = $e->{emoji}->[ int rand $e->{emoji}->@* ];
    my $slack  = $e->{slackname}->[ int rand $e->{slackname}->@* ];

    if ($event->from_channel->isa('Synergy::Channel::Slack')) {
      return $event->reply(
        $emoji,
        {
          slack_reaction => { event => $event, reaction => $slack },
        },
      );
    }

    if ($event->from_channel->isa('Synergy::Channel::Discord')) {
      $Logger->log("discord");
      return $event->reply(
        $emoji,
        {
          discord_reaction => { event => $event, reaction => $emoji },
        },
      );
    }

    if ($event->from_channel->isa('Synergy::Channel::Console')) {
      return $event->reply("[ pretend you got this cute reaction: $emoji ]");
    }

    # This is sort of a mess.  If someone addresses us from an unsupported
    # channel, we don't want to play dumb, but we don't want to give stupid
    # replies to SMS because they contained "cat pic" embedded in them.  So if
    # we're not Slack (and by this point we know we're not) and the message is
    # exactly a pic request, we'll give an emoji reply.
    $event->reply($emoji) if $exact;
    return;
  }

  return;
};

# Sometimes, respond in passing to a mention of "jazz" with a saxophone
# slackmoji. -- michael, 2019-02-06
listener jazz_pic => sub ($self, $event) {
  return unless $event->text =~ /jazz/i;
  return unless $event->from_channel->isa('Synergy::Channel::Slack');
  return unless rand() < 0.1;

  return $event->reply(
    "\N{SAXOPHONE}",
    {
      slack_reaction => { event => $event, reaction => 'saxophone' },
    },
  );

  return;
};

# TODO: we want a way to write some kind of custom prefix matching hybrid
# listener / command?
responder dog_pic => {
  exclusive => 1,
  targeted  => 1,
  help_titles => [ 'dog pic' ],
  help      => '*dog pic*: get a picture of a dog',
  matcher   => sub ($text, @) {
    return unless $text =~ /\Adog\s+pic\z/i
               || $text =~ /\Aunleash\s+the\s+hounds\z/i;
    return [];
  },
} => sub ($self, $event) {
  $event->mark_handled;

  my $http_future = $self->hub->http_get(
    "https://dog.ceo/api/breeds/image/random",
  );

  $http_future->on_done(sub ($res) {
    my $json = eval { JSON::MaybeXS->new->decode( $res->decoded_content ) };
    my $error = $@;

    if ($json && $json->{status} eq 'success') {
      $event->reply($json->{message});
      return;
    }

    $Logger->log("doggo error: $error") if $error;
    $event->reply("Something went wrong getting the doggos!");
  });

  return;
};

1;
